set nocompatible

""" Plugins
call plug#begin('~/.vim/plugged')
    Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
    Plug 'junegunn/fzf.vim'
    Plug 'scrooloose/nerdtree', { 'on':  'NERDTreeToggle' }
    Plug 'majutsushi/tagbar'
    Plug 'tpope/vim-fugitive' | Plug 'airblade/vim-gitgutter'
    Plug 'yuttie/comfortable-motion.vim'
    Plug 'christoomey/vim-tmux-navigator'
    Plug 'Lokaltog/vim-easymotion'
    Plug 'SirVer/ultisnips' | Plug 'honza/vim-snippets'
    Plug 'glacambre/firenvim', { 'do': { _ -> firenvim#install(1) } }
    Plug 'iamcco/markdown-preview.nvim', { 'do': 'cd app && yarn install'  }
    " UI
    Plug 'sainnhe/gruvbox-material'
    Plug 'arzg/vim-colors-xcode'
    Plug 'vim-airline/vim-airline'
    Plug 'ryanoasis/vim-devicons'
    Plug 'sheerun/vim-polyglot'
    Plug 'kyazdani42/nvim-web-devicons'
    " Text formating
    Plug 'Yggdroot/indentLine'
    Plug 'tpope/vim-commentary'
    Plug 'Raimondi/delimitMate'
    Plug 'godlygeek/tabular'
    Plug 'tpope/vim-surround'
    " LSP
    Plug 'prabirshrestha/vim-lsp' | Plug 'mattn/vim-lsp-settings'
    Plug 'prabirshrestha/asyncomplete.vim' | Plug 'prabirshrestha/asyncomplete-lsp.vim'
    Plug 'neoclide/coc.nvim', {'branch': 'release'}
    " Profile Vim startup
    Plug 'dstein64/vim-startuptime'
    Plug 'wakatime/vim-wakatime'
    " Jupyter
    Plug 'bfredl/nvim-ipy' | Plug 'hkupty/iron.nvim'
    Plug 'kana/vim-textobj-line' | Plug 'kana/vim-textobj-user'
    Plug 'GCBallesteros/vim-textobj-hydrogen' | Plug 'GCBallesteros/jupytext.vim'
    " Telescope
    Plug 'nvim-lua/popup.nvim'
    Plug 'nvim-lua/plenary.nvim'
    Plug 'nvim-telescope/telescope.nvim'
    " Python
    Plug 'fisadev/vim-isort'
call plug#end()

filetype plugin indent on       " Add filetype, plugin, and indent support
syntax on

set cursorline
"" Formating
set encoding=utf-8
set showcmd
set textwidth=79
set colorcolumn=80
set laststatus=2
set linespace=1
set smartindent
set updatetime=50
set mouse=a
" Give more space for displaying messages.
set cmdheight=1
set termguicolors
""" Whitespace
set nowrap                      " don't wrap lines
set tabstop=4                   " a tab is two spaces (or set this to 4)
set shiftwidth=4                   " a tab is two spaces (or set this to 4)
set expandtab                   " use spaces, not tabs (optional)
set smarttab
set backspace=indent,eol,start  " backspace through everything in insert mode
"" Searching
set hlsearch                   " highlight matches
set incsearch                  " incremental searching
set ignorecase                 " searches are case insensitive...
set smartcase                  " ... unless they contain at least one capital letter
set number
set relativenumber
set wildmenu
"" Swap/backup
set backupdir=~/tmp
set noswapfile
set nowritebackup
set nobackup

if exists('+termguicolors')
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" Gruvbox-material
let g:gruvbox_material_background = 'hard'
let g:gruvbox_material_enable_italic = 1
let g:gruvbox_material_enable_bold = 1
let g:gruvbox_material_better_performance = 1
let g:gruvbox_material_palette = 'mix'
let g:gruvbox_material_sign_column_background = 'none'

colorscheme gruvbox-material

let mapleader=" "
com! W w
inoremap <C-c> <esc>

if has('nvim')
    set clipboard+=unnamedplus
    let g:python_host_prog = "/Users/rbanyi/.pyenv/versions/neovim2/bin/python"
    let g:python3_host_prog = "/Users/rbanyi/.pyenv/versions/neovim3/bin/python"

    let g:firenvim_config = {
      \ 'globalSettings': {
          \ 'alt': 'all',
      \  },
      \ 'localSettings': {
          \ '.*': {
              \ 'cmdline': 'neovim',
              \ 'priority': 0,
              \ 'selector': 'textarea',
              \ 'takeover': 'always',
          \ },
      \ }
    \ }

    let fc = g:firenvim_config['localSettings']
    let fc['https://youtube.com.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https?://instagram.com.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https?://twitter.com.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https://.*gmail.com.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https://.*google.com.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https?://.*twitch.tv.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https?://notion.so.*'] = { 'takeover': 'never', 'priority': 1 }
    let fc['https?://reddit.com.*'] = { 'takeover': 'never', 'priority': 1 }
else
    set ttymouse=sgr
endif

" Join yanked text on a yank (needed for terminal mode copies)
vnoremap yy y<CR>:let @"=substitute(@", '\n', '', 'g')<CR>:call yank#Osc52Yank()<CR>

""" Move selected lines
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" Commonly-used files
nnoremap <leader>cv :e $MYVIMRC<CR>
nnoremap <leader>cz :e ~/.zshrc<CR>
nnoremap <leader>ce :e ~/.zshenv<CR>
nnoremap <leader>ca :e ~/.alias<CR>
nnoremap <leader>cf :e ~/.functions<CR>
nnoremap <leader>ct :e ~/.tmux.conf<CR>
nnoremap <leader>cy :e ~/.yabairc<CR>
nnoremap <leader>cs :e ~/.skhdrc<CR>

" Toggle Paste mode
nnoremap <leader>p :set paste!<CR>

" Adjusting splits
nnoremap <silent> <Leader>> :vertical resize +10<CR>
nnoremap <silent> <Leader>< :vertical resize -10<CR>
nnoremap <silent> <Leader>+ :resize +10<CR>
nnoremap <silent> <Leader>- :resize -10<CR>

" Window navigation
nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
nnoremap <C-H> <C-W><C-H>

let g:python_highlight_all = 1

"" utilsnips
let g:UltiSnipsExpandTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<c-b>"
let g:UltiSnipsJumpBackwardTrigger="<c-z>"

"" fzf
let g:fzf_layout = { 'window': { 'width': 0.8, 'height': 0.8 } }
let $FZF_DEFAULT_OPTS='--reverse'


"" Nerdtree
nmap <silent> <leader>n :NERDTreeToggle<CR>
nnoremap <silent> <Leader>f :NERDTreeFind<CR>
let g:NERDTreeDirArrowExpandable = ''
let g:NERDTreeDirArrowCollapsible = ''
let g:NERDTreeWinPos = "right"
let NERDTreeNaturalSort = 1
let NERDTreeIgnore = ['\.pyc$', '__pycache__']

"" Tagbar
let g:tagbar_left = 1
nmap <silent> <Leader>t :TagbarToggle<CR>

"" Git
nmap <silent> <Leader>gs :Gstatus<CR>
nmap <silent> <Leader>gvd :Gvdiffsplit<CR>
nmap <silent> <leader>gl :diffget //3<CR>
nmap <silent> <leader>gh :diffget //2<CR>

"" Move among buffers with CTRL
map <Leader>l :bnext<CR>
map <Leader>h :bprev<CR>

"" Airline
let g:airline_section_x = '%{airline#util#prepend(airline#extensions#tagbar#currenttag(),0)}'
let g:airline_section_y = ''
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#left_sep = ' '
let g:airline#extensions#tabline#left_alt_sep = ''
let g:airline#extensions#tabline#formatter = 'default'

""" Tabular
if exists(":Tabularize")
    nmap <Leader>= :Tab /=<CR>
    vmap <Leader>= :Tab /=<CR>
    nmap <Leader>: :Tab /:\zs<CR>
    vmap <Leader>: :Tab /:\zs<CR>
endif

" Autocompletion
set completeopt=menuone,noinsert,noselect
let g:asyncomplete_auto_completeopt = 0
let g:completion_matching_strategy_list = ['exact', 'substring', 'fuzzy']

inoremap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"
inoremap <expr> <cr>    pumvisible() ? asyncomplete#close_popup() : "\<cr>"
imap <c-space> <Plug>(asyncomplete_force_refresh)

""" LSP
if executable('pyls')
    " pip install python-language-server
    au User lsp_setup call lsp#register_server({
        \ 'name': 'pyls',
        \ 'cmd': {server_info->['pyls']},
        \ 'allowlist': ['python'],
        \ })
endif

function! s:on_lsp_buffer_enabled() abort
    setlocal omnifunc=lsp#complete
    setlocal signcolumn=yes
    if exists('+tagfunc') | setlocal tagfunc=lsp#tagfunc | endif
    nmap <buffer> gd <plug>(lsp-definition)
    nmap <buffer> gr <plug>(lsp-references)
    nmap <buffer> gi <plug>(lsp-implementation)
    nmap <buffer> gt <plug>(lsp-type-definition)
    nmap <buffer> <leader>rn <plug>(lsp-rename)
    nmap <buffer> [g <Plug>(lsp-previous-diagnostic)
    nmap <buffer> ]g <Plug>(lsp-next-diagnostic)
    nmap <buffer> K <plug>(lsp-hover)
    nmap <buffer> <Leader>fd :LspDocumentDiagnostics<CR>
    nmap <buffer> <Leader>ff :LspDocumentFormat<CR>
    " refer to doc to add more commands
    highlight link LspErrorText GruvboxRedSign
    highlight clear LspWarningLine
    let g:lsp_highlights_enabled = 0
    let g:lsp_textprop_enabled = 0
    let g:lsp_highlight_references_enabled = 1
endfunction

" easy motion
let g:comfortable_motion_scroll_down_key = "j"
let g:comfortable_motion_scroll_up_key = "k"


nnoremap <leader>ghw :h <C-R>=expand("<cword>")<CR><CR>
nnoremap <leader>pw :Rg <C-R>=expand("<cword>")<CR><CR>
nnoremap <leader>prw :CocSearch <C-R>=expand("<cword>")<CR><CR>

nnoremap <C-p> :lua require('telescope.builtin').git_files()<CR>
nnoremap <leader>fw :lua require('telescope.builtin').grep_string { search = vim.fn.expand("<cword>") }<CR>
nnoremap <leader>ff <cmd>lua require('telescope.builtin').find_files()<cr>
nnoremap <leader>fg <cmd>lua require('telescope.builtin').live_grep()<cr>
nnoremap <leader>fb <cmd>lua require('telescope.builtin').buffers()<cr>
nnoremap <leader>ft <cmd>lua require('telescope.builtin').help_tags()<cr>

nnoremap <Leader><CR> :so ~/.config/nvim/init.vim<CR>

" Jupyter

function! GetKernelFromPipenv()
    let l:kernel = tolower(system('basename $(pipenv --venv)'))
    " Remove control characters (most importantly newline)
    return substitute(l:kernel, '[[:cntrl:]]', '', 'g')
endfunction

function! StartConsolePipenv(console)
    let l:flags = '--kernel ' . GetKernelFromPipenv()
    let l:command=a:console . ' ' . l:flags
    call jobstart(l:command)
endfunction

function! AddFilepathToSyspath()
    let l:filepath = expand('%:p:h')
    call IPyRun('import sys; sys.path.append("' . l:filepath . '")')
    echo 'Added ' . l:filepath . ' to pythons sys.path'
endfunction

function! ConnectToPipenvKernel()
    let l:kernel = GetKernelFromPipenv()
    call IPyConnect('--kernel', l:kernel, '--no-window')
endfunction

function! GetPoetryVenv()
    let l:poetry_config = findfile('pyproject.toml', getcwd().';')
    let l:root_path = fnamemodify(l:poetry_config, ':p:h')
    if !empty(l:poetry_config)
        let l:virtualenv_path = system('cd '.l:root_path.' && poetry env list --full-path')
        return l:virtualenv_path
    endif
endfunction

function! GetPipenvVenv()
    let l:pipenv_config = findfile('Pipfile', getcwd().';')
    let l:root_path = fnamemodify(l:pipenv_config, ':p:h')
    if !empty(l:pipenv_config)
        let l:virtualenv_path = system('cd '.l:root_path.' && pipenv --venv 2>/dev/null')
        return l:virtualenv_path
    endif
    return ''
endfunction

function! GetPythonVenv()
    let l:venv_path = GetPoetryVenv()
    if empty(l:venv_path)
        let l:venv_path = GetPipenvVenv()
    endif
    if empty(l:venv_path)
        return v:null
    else
        return l:venv_path
    endif
endfunction

command! -nargs=0 RunQtPipenv call StartConsolePipenv('jupyter qtconsole')
command! -nargs=0 RunQtConsole call jobstart("jupyter qtconsole --existing")
command! -nargs=0 RunPipenvKernel terminal /bin/bash -i -c 'pipenv run python -m ipykernel'
command! -nargs=0 RunPoetryKernel terminal /bin/bash -i -c 'poetry run python -m ipykernel'
command! -nargs=0 ConnectToPipenvKernel call ConnectToPipenvKernel()
command! -nargs=0 ConnectConsole terminal /bin/bash -i -c 'jupyter console --existing'
command! -nargs=0 AddFilepathToSyspath call AddFilepathToSyspath()

autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

fun! TrimWhitespace()
    let l:save = winsaveview()
    keeppatterns %s/\s\+$//e
    call winrestview(l:save)
endfun

augroup lsp_install
    au!
    " call s:on_lsp_buffer_enabled only for languages that has the server registered.
    autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END

augroup richban
    autocmd!
    autocmd BufWritePre * :call TrimWhitespace()
    autocmd BufEnter,BufWinEnter,TabEnter *.rs :lua require'lsp_extensions'.inlay_hints{}
    au BufEnter github.com_*.txt set filetype=markdown
augroup END
